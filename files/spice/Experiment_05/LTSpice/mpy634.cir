* MPY634_BEHAVIORAL
*****************************************************************************
* (C) COPYRIGHT 2011 TEXAS INSTRUMENTS INCORPORATED. ALL RIGHTS RESERVED.
*****************************************************************************
** THIS MODEL IS DESIGNED AS AN AID FOR CUSTOMERS OF TEXAS INSTRUMENTS.
** TI AND ITS LICENSORS AND SUPPLIERS MAKE NO WARRANTIES, EITHER EXPRESSED
** OR IMPLIED, WITH RESPECT TO THIS MODEL, INCLUDING THE WARRANTIES OF
** MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.  THE MODEL IS
** PROVIDED SOLELY ON AN "AS IS" BASIS.  THE ENTIRE RISK AS TO ITS QUALITY
** AND PERFORMANCE IS WITH THE CUSTOMER.
*****************************************************************************
*
* THIS MODEL IS SUBJECT TO CHANGE WITHOUT NOTICE. TEXAS INSTRUMENTS
* INCORPORATED IS NOT RESPONSIBLE FOR UPDATING THIS MODEL.
*
*****************************************************************************
*
** RELEASED BY: ANALOG ELAB DESIGN CENTER, TEXAS INSTRUMENTS INC.
* PART: MPY634
* DATE: 31JUL2011
* MODEL TYPE: BEHAVIORAL
* SIMULATOR: TINA
* SIMULATOR VERSION: 9.1.30.94 SF -TI
* EVM ORDER NUMBER: N/A
* EVM USERS GUIDE: N/A
* DATASHEET: SBFS017A – DECEMBER 1995 – REVISED DECEMBER 2004
*
* MODEL VERSION: 1.0
*
*****************************************************************************
*
* UPDATES:
*
* VERSION 1.0
*
*****************************************************************************
* THE CIRCUIT BELOW MODELS THE BEHAVIOR OF THE MPY634 FOUR QUADRANT
* MULTIPLIER BY IMPLEMENTING THE FOLLOWING TRANSFER FUNCTION:
*
*            [(X1 - X2)*(Y1 - Y2)             ]
* VOUT = A * [-------------------  - (Z1 - Z2)]
*            [         SF                     ]
*
* THE SUPPLY PINS +VS & -VS ONLY ARE USED TO CLAMP THE OUTPUT VOLTAGE TO {-VS,+VS}
* THE SCALE FACTOR OR SF HAS TO BE PROVIDED AS A DC VOLTAGE TO THE MODEL
* THE VOLTAGE AT THE SF PIN CAN IS CLAMPED INTERNALLY TO {3,10} AND MAPPED TO {0.1,10}
* THE MODEL WAS CREATED ASSUMING A SUPPLY VOLTAGE OF +/- 15 V

.SUBCKT MPY634  X1 X2 SF Y1 Y2 +VS OUT Z1 Z2 -VS

ECS3        2 0 VALUE = {LIMIT(V(SF,0),3,10)}
ECS1        3 0 VALUE = {((V(4,0)*V(5,0))/V(VF1,0)-V(6,0))*17783}
ECS4        OUT 0 VALUE = {LIMIT(V(3,0),V(-VS,0),V(+VS,0))}
ECS2        VF1 0 VALUE = {V(2,0)*9.9/7-29/7}
EZ          6 0 Z1 Z2  1
EY          5 0 Y1 Y2  1
EX          4 0 X1 X2  1

.ENDS MPY634
